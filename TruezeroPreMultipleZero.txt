model{
for(i in 1:k){
y[i] ~ dbin(ap[i], n[i])
ap[i] <- sub.p[i]*main.Se + (1-sub.p[i])*(1-main.Sp)
sub.p[i] <- z[i] * pstar[i]
z[i] ~ dbern(main.tau0)

#informative hyperpior for prevalence
pstar[i] ~ dbeta(alpha,beta)
}
alpha <- main.ap*psi
beta <- psi*(1-main.ap)
main.ap ~ dbeta(amu, bmu) I(0.01,0.99) ## Mode=0.12, 95% sure < 0.30  dbeta(3.283, 17.744) 
psi ~ dgamma(apsi, bpsi) I(0.01,0.99) ## Uses Median of 95th percentile of prevalence dgamma(4.524, 0.387)

#informative prior for Se and Sp
main.Se ~ dbeta(ase, bse) I(0.01,0.99) ## Mode=0.25; 95% sure < 0.30
main.Sp ~ dbeta(asp, bsp) I(0.01,0.99) ## Mode=0.98, 95% sure > 0.96

#informative prior for the probability of zero between group/herd prevalence
main.tau0 ~ dbeta(atau0, btau0)## Mode=0.60, 95% sure < 0.827

#predictions
pre.z.rep ~ dbern(main.tau0)
pre.pstar.rep ~ dbeta(alpha,beta)
pre.p.rep <- pre.z.rep*pre.pstar.rep
pre.pequal0 <- equals(pre.p.rep,0)
pre.plessthan0.05 <- step(0.05-pre.p.rep)
}